<p><a href=category:general>category:general</a> <a href=category:scroll>category:scroll</a></p><h2 id=definition>Definition</h2><p>A <a href=scroll_window title=wikilink>scroll window</a> is a
<a href=region title=wikilink>region</a> in which a scroll has been started by
<a href=start_scroll title=wikilink>start_scroll</a>() and setup with the <a href=global_variable title=wikilink>global
variable</a> <a href=scroll title=wikilink>scroll</a>. This
scroll window provides a view window into the scroll that has been setup
there, with certain background and foreground
<a href=graphics title=wikilink>graphics</a>. The view window can be &lsquo;scrolled&rsquo; over
the scroll (and the graphics).</p><p>The use of scrolls can be various, but mostly they are used for
scrolling games, like
<a href=http://en.wikipedia.org/wiki/Side-scrolling_video_game>sidescrollers</a>.</p><p><a href=Process title=wikilink>Processes</a> can also be added to a scroll (see the
<a href=global_variable title=wikilink>global variable</a>
<a href=ctype title=wikilink>ctype</a>), in which case a lot of things related to
that process change.</p><ul><li>The <a href=graphic title=wikilink>graphic</a> of the process is not drawn to the
screen, but to the scroll window, using the
<a href=region title=wikilink>region</a> of the scroll.</li><li>The <a href=z title=wikilink>z</a> of an added process only has a meaning to the
scroll window and compared to other processes added to the same
scroll, meaning from a user perspective, the process is drawn at the
z value of the scroll. If two processes are added to the same
scroll, the one with the lower z value will be drawn over the
other one.</li><li>The <a href=x title=wikilink>x</a>- and <a href=y title=wikilink>y</a>-coordinates are not
relative to the upper left corner of the <em>screen</em>, but relative to
the upper left corner of the <em>scroll</em>.</li></ul><p>You can control scrolls in multiple ways.</p><ul><li>Change the scroll coordinates directly using <code>x0</code>, <code>y0</code>, <code>x1</code>, <code>y1</code>
in the <a href=global_variable title=wikilink>global variable</a>
<a href=scroll title=wikilink>scroll</a>. Be sure to alter the correct scroll.</li><li>Tell the scroll to &lsquo;follow&rsquo; another scroll, by using
<code>[[scroll]].follow</code>.</li><li>Use a &lsquo;camera&rsquo; process, which the scroll try to follow, by using
<code>[[scroll]].camera</code>.</li></ul><p>More about scrolls:</p><ul><li>A scroll with a lower z value will be drawn over a scroll with a
lower z value.</li><li>The foreground is the plane to be controlled and the background
moves relative to the foreground.</li></ul><h2 id=example>Example</h2><pre><code>Const
    SCREEN_WIDTH = 320;
    SCREEN_HEIGHT = 200;
    SCREEN_DEPTH = 8;
    MAP_WIDTH  = 500;
    MAP_HEIGHT = 400;
    SHIP_WIDTH  = 30;
    SHIP_HEIGHT = 30;
    SHIP_SPEED = 6;
End

Declare Process playership(int x, int y)
End
Declare Function int makeMap(int width, int height, int depth)
End
Declare Function int makeShipGfx(int width, int height, int depth)
End

Process Main()
Private
    int map;
Begin
    // Setup the screen
    set_mode(SCREEN_WIDTH,SCREEN_HEIGHT,SCREEN_DEPTH);

    // Create the background graphic
    map = makeMap(MAP_WIDTH,MAP_HEIGHT,SCREEN_DEPTH);

    // Start a new scroll
    // scrollNumber = 0
    // fileID = 0
    // foregroundGraphID = map
    // backgroundGraphID = 0: no background
    // region = 0: use entire screen
    // lockindicator = 0: don't repeat graphics
    start_scroll(0,0,map,0,0,0);

    // Start a controllable object on the scroll
    playership(320,200);

    // Wait till the key ESC is pressed
    Repeat
        frame;
    Until(key(_esc))

OnExit
    // Kill all other processes
    let_me_alone();

    // Clean up memory used for map
    unload_map(0,map);
End


Process playership(int x, int y)
Private
    int sp = SHIP_SPEED;
    int halfWidth;
    int halfHeight;
Begin

    // This process should be displayed on a scroll only
    ctype = c_scroll;

    // This process should be tracked by the scroll
    Scroll.camera = id;

    // Create a graph for this process
    graph = makeShipGfx(SHIP_WIDTH,SHIP_HEIGHT,SCREEN_DEPTH);

    // Obtain the value of half the width of the graphic
    halfWidth  = graphic_info(0,graph,G_WIDTH )/2;
    halfHeight = graphic_info(0,graph,G_HEIGHT)/2;

    // React on keys LEFT and RIGHT
    Loop

        // Adjust x,y on input
        x+=(key(_right)-key(_left))*sp;
        y+=(key(_down)-key(_up))*sp;

        // Make sure the ship stays on the scroll
        if(x &gt; MAP_WIDTH-halfWidth)
            x = MAP_WIDTH-halfWidth;
        elseif(x &lt; halfWidth)
            x = halfWidth;
        end
        if(y &gt; MAP_HEIGHT-halfHeight)
            y = MAP_HEIGHT-halfHeight;
        elseif(y &lt; halfHeight)
            y = halfHeight;
        end

        frame;

    End

OnExit
    // Clean up used memory for graph
    unload_map(0,graph);
End

Function int makeMap(int width, int height, int depth)
Private
    int n;
    int x1,y1;
    int m;
Begin

    // New map
    m = new_map(width,height,depth);

    // Put on some pretty stars
    rand_seed(100);
    for(n=0;n&lt;1000;n++)
        map_put_pixel(0,m,rand(0,width),rand(0,height),rgb(150,170,200));
    end

    // Return it
    return m;

End

Function int makeShipGfx(int width, int height, int depth)
Private
    int g;
Begin

    // Return a map of one color (RGB(150,200,170))
    g=new_map(width,height,depth);
    map_clear(0,g,rgb(200,100,170));
    return g;

End
</code></pre><p>Used in example: <a href=set_mode title=wikilink>set_mode</a>(),
<a href=start_scroll title=wikilink>start_scroll</a>(), <a href=key title=wikilink>key</a>(),
<a href=let_me_alone title=wikilink>let_me_alone</a>(),
<a href=unload_map title=wikilink>unload_map</a>(),
<a href=graphic_info title=wikilink>graphic_info</a>(),
<a href=new_map title=wikilink>new_map</a>(), <a href=rand_seed title=wikilink>rand_seed</a>(),
<a href=rand title=wikilink>rand</a>(), <a href=rgb title=wikilink>rgb</a>(),
<a href=map_put_pixel title=wikilink>map_put_pixel</a>(),
<a href=map_clear title=wikilink>map_clear</a>(), <a href=ctype title=wikilink>ctype</a>,
<a href=scroll title=wikilink>scroll</a></p>